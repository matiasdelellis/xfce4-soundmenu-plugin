dnl $Id$
dnl
dnl xfce4-soundmenu-plugin - A mpris2 remote control plugin for the Xfce panel
dnl based on xfce4-sample-plugin thanks to John Doo <john@foo.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([soundmenu_version_major], [0])
m4_define([soundmenu_version_minor], [6])
m4_define([soundmenu_version_micro], [0])
m4_define([soundmenu_version], [soundmenu_version_major().soundmenu_version_minor().soundmenu_version_micro()])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2011-2013
        Matias De lellis. All rights reserved.])
AC_INIT([xfce4-soundmenu-plugin], [soundmenu_version], [http://bugzilla.xfce.org/], [xfce4-soundmenu-plugin])
AC_PREREQ([2.50])
AC_REVISION([xfce4_panel_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2 tar-ustar])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_AIX()
AC_ISC_POSIX()

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AM_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()
AC_CHECK_HEADERS([stdlib.h unistd.h locale.h stdio.h errno.h time.h string.h \
                  math.h sys/types.h sys/wait.h memory.h signal.h sys/prctl.h \
                  libintl.h])
AC_CHECK_FUNCS([bind_textdomain_codeset])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl *******************************
dnl *** Check for X11 installed ***
dnl *******************************
XDT_CHECK_LIBX11_REQUIRE()

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.20.0])
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.28])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-1], [4.9.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.9.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.9.0])
XDT_CHECK_PACKAGE([LIBMPRIS2CLIENT], [libmpris2client], [0.0.1])

dnl Check global keyboard shortcuts support
LIBKEYBINDER_FOUND="no"
XDT_CHECK_OPTIONAL_PACKAGE([LIBKEYBINDER],
                       [keybinder], [0.2.2],
                       [keybinder],
                       [keybinder library], [yes])
dnl Check libclastfm
LIBCLASTFM_FOUND="no"
XDT_CHECK_OPTIONAL_PACKAGE([LIBCLASTFM],
                       [libclastfm], [0.5],
                       [libclastfm],
                       [libclastfm library], [yes])
dnl Check libglyr
LIBGLYR_FOUND="no"
XDT_CHECK_OPTIONAL_PACKAGE([LIBGLYR],
                       [libglyr], [0.9.4],
                       [libglyr],
                       [libglyr library], [yes])

dnl Check notification support
LIBNOTIFY_FOUND="no"
XDT_CHECK_OPTIONAL_PACKAGE([LIBNOTIFY],
                       [libnotify], [0.4.4],
                       [libnotify],
                       [libnotify library], [yes])

PULSE_MIN_VERS=2.0
PKG_CHECK_MODULES(GVC, libpulse >= $PULSE_MIN_VERS libpulse-mainloop-glib gobject-2.0)
AM_CONDITIONAL([HAVE_INTROSPECTION], 0)

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

dnl *********************************
dnl *** Substitute platform flags ***
dnl *********************************
AC_MSG_CHECKING([PLATFORM_CPPFLAGS])
AC_MSG_RESULT([$PLATFORM_CPPFLAGS])
AC_SUBST([PLATFORM_CPPFLAGS])
AC_MSG_CHECKING([PLATFORM_CFLAGS])
AC_MSG_RESULT([$PLATFORM_CFLAGS])
AC_SUBST([PLATFORM_CFLAGS])
AC_MSG_CHECKING([PLATFORM_LDFLAGS])
AC_MSG_RESULT([$PLATFORM_LDFLAGS])
AC_SUBST([PLATFORM_LDFLAGS])

AC_OUTPUT([
Makefile
icons/Makefile
icons/128x128/Makefile
panel-plugin/Makefile
gvc/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "xfce4-soundmenu-plugin Build Configuration:"
echo
echo "  Debug Support...............................................................: $enable_debug"
echo "  Global keyboard shortcuts support...........................................: $LIBKEYBINDER_FOUND"
echo "  Lastfm support (Need http://liblastfm.sourceforge.net >=0.5)................: $LIBCLASTFM_FOUND"
echo "  Search lyrics and artist info( Need https://github.com/sahib/glyr >= 0.9.4).: $LIBGLYR_FOUND"
echo "  Notification support........................................................: $LIBNOTIFY_FOUND"
echo


